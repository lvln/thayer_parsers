
json: element ;

value: object | array | STRING | number | "true" | "false" | "null" ;

object: '{' ws '}' | '{' members '}'  ;

members: member | members ',' member ;

member: ws STRING ws ':' element ;

array: '[' ws ']' | '[' elements ']' ;

elements: element | elements ',' element ;

element: ws value ws ;

STRING: '"' chars '"' ;

chars: /* empty */ | chars CHAR	;

CHAR: uchar | lchar | digit | punct | '\\' escchar ;

number: integer fraction exponent ;

integer: digit | onenine digits | '-' digit | '-' onenine digits ;

fraction: /* empty */ | '.' digits ;

exponent: /* empty */ | 'E' sign digits | 'e' sign digits ;

sign: /* empty */ | '+' | '-' ;

digits: digit | digits digit ;

digit: '0' | onenine ;

onenine: ['1'-'9'] ;

ws: /* empty */	| ws ' ' | ws '\n' | ws '\t' | ws '\r' ;

escchar: 'b' | 'f' | 'n' | 'r' | 't' | '"' | '\\' | '/' | 'u' hex hex hex hex ;

punct: ' ' | '!' | ['#'-'/'] | [':'-'@'] | '[' | [']'-'`'] | ['{'-'\x7f'] ; 

lchar: lhex | ['g' -'z'] ;                                                                                        
                                                                                                                  
uchar: uhex | ['G'-'Z'] ;                                                                                         
                                                                                                                  
uhex: ['A'-'F'] ;                                                                                                 
                                                                                                                  
lhex: ['a'-'f'] ;                                                                                                 
                                                                                                                  
hex: digit | uhex | lhex ;
